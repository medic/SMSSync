apply plugin: 'android'

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: '*.jar')
    compile 'com.actionbarsherlock:actionbarsherlock:4.4.0@aar'
    compile 'com.android.support:support-v4:22.0.0'
    compile 'com.squareup:otto:1.3.4'
}

def isReleaseBuild = {
	return System.env.JENKINS == 'true' || (System.env.TRAVIS == 'true' &&
			System.env.TRAVIS_BRANCH == 'master')
}
def getVersionCode = {
	if(isReleaseBuild()) {
		def code = System.env.TRAVIS == 'true' ?
			System.env.TRAVIS_BUILD_NUMBER :
			System.env.VERSION_TO_BUILD.split(/\./)[-1]
		return Integer.parseInt(code)
	}
	return 1
}
def getVersionName = {
	if(isReleaseBuild()) {
		if(System.env.TRAVIS == 'true') {
			def v = System.env.TRAVIS_BUILD_NUMBER
			if(!v) throw new GradleException(
					'Missing required env var: TRAVIS_BUILD_NUMBER')
			return "medic_2.4.$v"
		} else return System.env.VERSION_TO_BUILD
	}
	return 'SNAPSHOT'
}

android {
    compileSdkVersion 17
    buildToolsVersion "21.1.1"

    sourceSets {
        main {
            manifest.srcFile 'src/main/AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
            res.srcDirs = ['src/main/res']
        }

        defaultConfig {
            applicationId 'org.medicmobile.gateway.smssync'
            testApplicationId 'org.medicmobile.gateway.smssync.test'
        }

	// For legacy reasons, what would normally be src/androidTest is instead src/instrumentTest
	androidTest.setRoot('src/instrumentTest')

        // Move the build types to build-types/<type>
        // For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
        // This moves them out of them default location under src/<type>/... which would
        // conflict with src/ being used by the main source set.
        // Adding new build types or product flavors should be accompanied
        // by a similar customization.
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    signingConfigs {
        releaseSign
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releaseSign
            proguardFile getDefaultProguardFile('proguard-android.txt')
        }

        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-DEBUG"
        }
    }

    defaultConfig {
        targetSdkVersion 18
        minSdkVersion 8
        versionCode getVersionCode()
        versionName getVersionName()
        archivesBaseName = "${project.name}-${versionName}"
    }

    android.applicationVariants.all { variant ->
        // add google analytics code here. This is defined in the gradle.properties file
        variant.mergeResources.doLast {
            if (project.hasProperty('googleAnalyticsCode')) {
                File valuesFile =
                        file("${buildDir}/res/all/${variant.dirName}/values/values.xml")
                def analyticsCode = "<string name=\"ga_trackingId\">${googleAnalyticsCode}</string>"

                String content = valuesFile.getText('UTF-8')
                content = content.replaceAll("<string name=\"ga_trackingId\"></string>",
                        analyticsCode)
                valuesFile.write(content, 'UTF-8')
            }
        }
    }

    lintOptions {
	abortOnError false
    }
}

if (project.hasProperty('releaseKeyStore') &&
        project.hasProperty('releaseKeyPassword') &&
        project.hasProperty('releaseKeyStorePassword') &&
        project.hasProperty('releaseKeyAlias')) {
    android.signingConfigs.releaseSign.keyAlias = releaseKeyAlias
    android.signingConfigs.releaseSign.storeFile = file(releaseKeyStore)
    android.signingConfigs.releaseSign.storePassword = releaseKeyStorePassword
    android.signingConfigs.releaseSign.keyPassword = releaseKeyPassword
} else {
    android.signingConfigs.releaseSign.keyAlias ""
    android.signingConfigs.releaseSign.storeFile signingConfigs.debug.storeFile
    android.signingConfigs.releaseSign.storePassword ""
    android.signingConfigs.releaseSign.keyPassword ""
}
